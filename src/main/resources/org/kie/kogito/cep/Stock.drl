/**
 * Copyright 2021 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package org.kie.kogito.cep;
unit StockUnit;

import org.kie.kogito.cep.StockTick;
import org.kie.kogito.cep.ValueDrop;

rule CreateValueDrops when
    $s1: /stockTicks
    $s2: /stockTicks[ company == $s1.company, value < $s1.value, this after[1, 100] $s1 ] over window:length( 2 )
then
    valueDrops.add( new ValueDrop($s1.getCompany(), $s1.getValue(), $s2.getValue()) );
end

// TODO: at the moment it is not possible to pass arguments to a query invoked through the rule unit instance
query highestValueDrop(String $company)
     $s: /valueDrops[ company == $company ]
     not /valueDrops[ company == $company, dropAmount > $s.dropAmount ]
end
